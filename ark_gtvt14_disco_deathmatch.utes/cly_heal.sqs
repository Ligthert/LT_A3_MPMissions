;CLY Heal - a self-bandaging script written by Celery.
;
;Healing can be interrupted with movement keys and primary mouse button.
;
;-How to use-
;Execute in init script: [player,0.2,0.15,3] exec "cly_heal.sqs";
;The first number is the damage threshold after which you are given the option to heal.
;The second one is the player's damage after healing.
;The third one is the number of uses. -1 is unlimited.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

_unit=_this select 0
_target=_this select 1
?_unit!=player:exit

?_unit!=player or isDedicated:exit
?_this select 3!=0.235:goto "init"

_weapontype=getNumber (configFile/"CfgWeapons"/(currentWeapon _unit)/"type")

_anim="ainvpknlmstpsnonwnondnon_medic_2"
?_weapontype==1 or _weapontype==5:_anim="ainvpknlmstpslaywrfldnon_medic"
?_weapontype==2:_anim="ainvpknlmstpsnonwnondnon_medic_1"
_unit playMove _anim
CLY_healcooldown=time+5

@(animationState _unit in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"])
@!(animationState _unit in ["ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_medic_2"]) or CLY_healinterrupt
?CLY_healinterrupt:goto "interrupt"

?alive _unit and damage _unit>CLY_healthreshold:_unit setDamage CLY_healdamage;CLY_healings=CLY_healings-1
?!canStand _unit:_unit setHit ["legs",CLY_healdamage];CLY_healings=CLY_healings-1
exit

#interrupt
?_weapontype==1 or _weapontype==5:[-2, { _this switchMove 'ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon'; _this playMoveNow 'ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon'; }, _unit] call CBA_fnc_globalExecute;exit
?_weapontype==2:[-2, { _this switchMove 'ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon'; _this playMoveNow 'ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon'; }, _unit] call CBA_fnc_globalExecute;exit
?_weapontype==4:[-2, { _this switchMove 'amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon'; _this playMoveNow 'amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon'; }, _unit] call CBA_fnc_globalExecute;exit
[-2, { _this switchMove 'ainvpknlmstpslaywrfldnon_amovpknlmstpsnonwnondnon'; _this playMoveNow 'ainvpknlmstpslaywrfldnon_amovpknlmstpsnonwnondnon'; }, _unit] call CBA_fnc_globalExecute
exit



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



#init
CLY_healthreshold=_this select 1
CLY_healdamage=_this select 2
CLY_healings=_this select 3
CLY_healinterrupt=false
CLY_healanims=["amovpknlmstpsraswrfldnon_ainvpknlmstpslaywrfldnon","ainvpknlmstpslaywrfldnon_medic","ainvpknlmstpslaywrfldnon_amovpknlmstpsraswrfldnon","amovpknlmstpsraswpstdnon_ainvpknlmstpsnonwnondnon","amovpknlmstpsraswpstdnon_ainvpknlmstpsnonwnondnon_end","ainvpknlmstpsnonwnondnon_medic_1","ainvpknlmstpsnonwnondnon_amovpknlmstpsraswpstdnon","amovpknlmstpsraswlnrdnon_ainvpknlmstpsnonwnondnon","ainvpknlmstpsnonwnondnon_medic_2","amovpknlmstpsnonwnondnon_amovpknlmstpsraswlnrdnon"]
CLY_healcooldown=time-5
CLY_healid=str random 10000

~0.1

;Interrupt keys
CLY_key_healinterrupt=actionKeys "fire"+actionKeys "moveForward"+actionKeys "moveFastForward"+actionKeys "moveBack"+actionKeys "moveLeft"+actionKeys "moveRight"+actionKeys "turnLeft"+actionKeys "turnRight"
findDisplay 46 displayAddEventHandler ["KeyDown","if ((_this select 1) in CLY_key_healinterrupt) then {CLY_healinterrupt=true}"]
findDisplay 46 displayAddEventHandler ["KeyUp","if ((_this select 1) in CLY_key_healinterrupt) then {CLY_healinterrupt=false}"]
findDisplay 46 displayAddEventHandler ["MouseButtonDown","if ((_this select 1)==0) then {CLY_healinterrupt=true}"]
findDisplay 46 displayAddEventHandler ["MouseButtonUp","if ((_this select 1)==0) then {CLY_healinterrupt=false}"]

;Action menu item loop
#action
_unit=player
_unit setVariable ["CLY_healid",CLY_healid,true]
_healings=CLY_healings
_leftstring=""
?CLY_healings>1:_leftstring=format [" (%1 uses)",CLY_healings]
?CLY_healings==1:_leftstring=" (1 use)"
CLY_heal=_unit addAction [format ["Bandage wounds%1",_leftstring],"cly_heal.sqs",0.235,0.1,true,true,"",format ["(damage player>CLY_healthreshold or !canStand player) and !(animationState player in CLY_healanims) and time>CLY_healcooldown and (CLY_healings>0 or CLY_healings<0) and (player getVariable 'CLY_healid')=='%1'",CLY_healid]]

@!alive _unit or _unit!=player or CLY_healings!=_healings
_unit removeAction CLY_heal
_unit setVariable ["CLY_healid",nil,true]
@alive player
goto "action"